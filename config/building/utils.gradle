/**
 * Provides utility methods for building.
 * */

def resolveSourceDirs(String buildType, String buildVariant) {
    def sourceDirs = [
            "${projectDir}/src/main/java",
    ]

    if (!buildType.isEmpty()) {
        sourceDirs += "${projectDir}/src/${buildType}/java"
    }

    def buildVariantDir = "${projectDir}/src/${buildVariant}/java"
    if (!sourceDirs.contains(buildVariantDir)) {
        sourceDirs += buildVariantDir
    }

    return sourceDirs
}

def resolveClassesDir(String buildType, String buildVariant) {
    if (!buildType.isEmpty()) {
        return "${buildDir}/tmp/kotlin-classes/${buildType}"
    } else {
        return "${buildDir}/tmp/kotlin-classes/${buildVariant}"
    }
}

def removeCheckTaskIf(taskName, taskType) {
    def checkDependencyTaskName = "${taskName}Test"
    def checkDependencyTask = tasks.findByName(checkDependencyTaskName)

    // remove all instances of the task
    tasks.removeIf { task ->
        task.name.contains(taskName)
    }

    // redefine deprecated check dependency task (for VerificationTasks)
    if (checkDependencyTask != null) {
        // deprecate this check task (unfortunately we are unable to remove it)
        task(checkDependencyTaskName, type: taskType, overwrite: true) {
            group = null
            description = "Deprecated. This does nothing"
        }
    }
}

ext {
    resolveSourceDirs = this.&resolveSourceDirs
    resolveClassesDir = this.&resolveClassesDir
    removeCheckTaskIf = this.&removeCheckTaskIf
}
